{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# A Wright-Fisher simulation implemented in C via Cython.\n",
    "\n",
    "OMG!\n",
    "\n",
    "We would use GSL via CythonGSL, but that would require a GPL license for this notebook, but we're doing CCBY."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext Cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "/Users/kevin/anaconda3/lib/python3.5/site-packages/h5py/__init__.py:36: FutureWarning: Conversion of the second argument of issubdtype from `float` to `np.floating` is deprecated. In future, it will be treated as `np.float64 == np.dtype(float).type`.\n",
      "  from ._conv import register_converters as _register_converters\n"
     ]
    }
   ],
   "source": [
    "import msprime\n",
    "import numpy as np"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -lgsl -lgslcblas -lm\n",
    "\n",
    "import msprime\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from cython.view cimport array as cvarray\n",
    "from libc.stdlib cimport malloc, realloc, free\n",
    "from libc.stdint cimport int32_t, uint32_t\n",
    "\n",
    "from cython_gsl.gsl_rng cimport gsl_rng,gsl_rng_mt19937,gsl_rng_alloc,gsl_rng_free,gsl_rng_set\n",
    "from cython_gsl.gsl_rng cimport gsl_rng_uniform\n",
    "from cython_gsl.gsl_random cimport gsl_ran_flat\n",
    "from cython_gsl.gsl_random cimport gsl_ran_poisson\n",
    "\n",
    "cdef int32_t * malloc_int32_t(size_t n):\n",
    "    return <int32_t*>malloc(n*sizeof(int32_t))\n",
    "\n",
    "cdef int32_t * realloc_int32_t(void * x, size_t n):\n",
    "    return <int32_t*>realloc(x,n*sizeof(int32_t))\n",
    "\n",
    "cdef double * malloc_double(size_t n):\n",
    "    return <double*>malloc(n*sizeof(double))\n",
    "\n",
    "cdef double * realloc_double(double * x, size_t n):\n",
    "    return <double*>realloc(<double *>x,n*sizeof(double))\n",
    "\n",
    "cdef struct Mutations:\n",
    "    double * pos\n",
    "    int32_t * time\n",
    "    size_t next_mutation, capacity\n",
    "    \n",
    "cdef int init_Mutations(Mutations * m):\n",
    "    m.next_mutation = 0\n",
    "    m.capacity = 10000\n",
    "    m.pos = malloc_double(m.capacity)\n",
    "    if m.pos == NULL:\n",
    "        return -1\n",
    "    m.time = malloc_int32_t(m.capacity)\n",
    "    if m.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef int realloc_Mutations(Mutations * m):\n",
    "    m.capacity *= 2\n",
    "    m.pos = realloc_double(m.pos,\n",
    "                          m.capacity)\n",
    "    if m.pos == NULL:\n",
    "        return -1\n",
    "    m.time = realloc_int32_t(m.time,\n",
    "                            m.capacity)\n",
    "    if m.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef void free_Mutations(Mutations * m):\n",
    "    free(m.pos)\n",
    "    free(m.time)\n",
    "    m.next_mutation = 0\n",
    "    m.capacity = 10000\n",
    "    \n",
    "cdef int add_mutation(double pos,\n",
    "                     int32_t generation,\n",
    "                     Mutations * m):\n",
    "    cdef int rv = 0\n",
    "    if m.next_mutation+1 >= m.capacity:\n",
    "        rv = realloc_Mutations(m)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    m.pos[m.next_mutation] = pos\n",
    "    m.time[m.next_mutation] = generation\n",
    "    m.next_mutation+=1\n",
    "    return rv\n",
    "    \n",
    "cdef struct Nodes:\n",
    "    double * time\n",
    "    size_t next_node, capacity\n",
    "    \n",
    "cdef int init_Nodes(Nodes * n):\n",
    "    n.next_node = 0\n",
    "    n.capacity = 10000\n",
    "    n.time = malloc_double(n.capacity)\n",
    "    if n.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef int realloc_Nodes(Nodes * n):\n",
    "    n.capacity *= 2\n",
    "    n.time = realloc_double(n.time,\n",
    "                            n.capacity)\n",
    "    if n.time == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "    \n",
    "cdef void free_Nodes(Nodes * n):\n",
    "    if n.time != NULL:\n",
    "        free(n.time)\n",
    "    n.next_node = 0\n",
    "    n.capacity = 10000\n",
    "\n",
    "cdef int add_node(double t, Nodes *n):\n",
    "    cdef int rv = 0\n",
    "    if n.next_node >= n.capacity:\n",
    "        rv = realloc_Nodes(n)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    n.time[n.next_node] = t\n",
    "    n.next_node+=1\n",
    "    return rv\n",
    "    \n",
    "cdef struct Edges:\n",
    "    double *left\n",
    "    double *right\n",
    "    int32_t *parent\n",
    "    int32_t *child\n",
    "    size_t next_edge, capacity\n",
    "    \n",
    "cdef int init_Edges(Edges * e):\n",
    "    e.next_edge = 0\n",
    "    e.capacity = 10000\n",
    "    e.left = malloc_double(e.capacity)\n",
    "    if e.left == NULL:\n",
    "        return -1\n",
    "    e.right = malloc_double(e.capacity)\n",
    "    if e.right == NULL:\n",
    "        return -1\n",
    "    e.parent = malloc_int32_t(e.capacity)\n",
    "    if e.parent == NULL:\n",
    "        return -1\n",
    "    e.child = malloc_int32_t(e.capacity)\n",
    "    if e.child == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "   \n",
    "cdef int realloc_Edges(Edges * e):\n",
    "    e.capacity *= 2\n",
    "    e.left = realloc_double(e.left,e.capacity)\n",
    "    if e.left == NULL:\n",
    "        return -1\n",
    "    e.right = realloc_double(e.right,e.capacity)\n",
    "    if e.right == NULL:\n",
    "        return -1\n",
    "    e.parent = realloc_int32_t(e.parent,e.capacity)\n",
    "    if e.parent == NULL:\n",
    "        return -1\n",
    "    e.child = realloc_int32_t(e.child,e.capacity)\n",
    "    if e.child == NULL:\n",
    "        return -1\n",
    "    return 0\n",
    "\n",
    "cdef void free_Edges(Edges * e):\n",
    "    free(e.left)\n",
    "    free(e.right)\n",
    "    free(e.parent)\n",
    "    free(e.child)\n",
    "    e.next_edge = 0\n",
    "    e.capacity = 10000\n",
    "    \n",
    "cdef int add_edge(double left, double right,\n",
    "             int32_t parent, int32_t child,\n",
    "             Edges * edges):\n",
    "    cdef int rv=0\n",
    "    if edges.next_edge+1 >= edges.capacity:\n",
    "        rv = realloc_Edges(edges)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "        \n",
    "    edges.left[edges.next_edge] = left\n",
    "    edges.right[edges.next_edge] = right\n",
    "    edges.parent[edges.next_edge] = parent\n",
    "    edges.child[edges.next_edge] = child\n",
    "    edges.next_edge += 1\n",
    "    return rv\n",
    "\n",
    "cdef struct Tables:\n",
    "    Nodes nodes\n",
    "    Edges edges\n",
    "    Mutations mutations\n",
    "    gsl_rng * rng\n",
    "    \n",
    "cdef int init_Tables(Tables * t, int seed):\n",
    "    cdef int rv = 0\n",
    "    rv = init_Nodes(&t.nodes)\n",
    "    if rv != 0:\n",
    "        return rv\n",
    "    rv = init_Edges(&t.edges)\n",
    "    if rv != 0:\n",
    "        return rv\n",
    "    rv = init_Mutations(&t.mutations)\n",
    "    if rv != 0:\n",
    "        return rv\n",
    "    t.rng = gsl_rng_alloc(gsl_rng_mt19937)\n",
    "    if t.rng == NULL:\n",
    "        return -1\n",
    "    gsl_rng_set(t.rng, seed)\n",
    "    return rv\n",
    "\n",
    "cdef void free_Tables(Tables * t):\n",
    "    free_Nodes(&t.nodes)\n",
    "    free_Edges(&t.edges)\n",
    "    free_Mutations(&t.mutations)\n",
    "    gsl_rng_free(t.rng)\n",
    "    \n",
    "cdef int infsites(double mu, int32_t generation,\n",
    "                  Tables * tables,\n",
    "                  dict lookup):\n",
    "    cdef unsigned nmut = gsl_ran_poisson(tables.rng, mu)\n",
    "    cdef unsigned i = 0\n",
    "    cdef double pos\n",
    "    cdef int rv = 0\n",
    "    for i in range(nmut):\n",
    "        pos = gsl_rng_uniform(tables.rng)\n",
    "        while pos in lookup:\n",
    "            pos = gsl_rng_uniform(tables.rng)\n",
    "        rv = add_mutation(pos,\n",
    "                         generation,\n",
    "                         &tables.mutations)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "        lookup[pos] = True\n",
    "    return rv\n",
    "\n",
    "cdef int poisson_recombination(double r,\n",
    "                               size_t pg1, size_t pg2,\n",
    "                                int32_t next_offspring_id,\n",
    "                               Tables * tables):\n",
    "    cdef unsigned nbreaks = gsl_ran_poisson(tables.rng, r)\n",
    "    cdef list b = []\n",
    "    cdef size_t lenb\n",
    "    cdef unsigned i = 0\n",
    "    cdef double x\n",
    "    # cdef list pgams\n",
    "    cdef int rv = 0\n",
    "    cdef double left,right\n",
    "    cdef int32_t p\n",
    "    if nbreaks == 0:\n",
    "        # The parent passes the entire region onto the child\n",
    "        rv = add_edge(0.0,1.0,pg1,\n",
    "                      next_offspring_id,&tables.edges)\n",
    "        if rv != 0:\n",
    "            return rv\n",
    "    else:\n",
    "        while i < nbreaks:\n",
    "            x = gsl_rng_uniform(tables.rng)\n",
    "            while x in b:\n",
    "                x = gsl_rng_uniform(tables.rng)\n",
    "            b.append(x)\n",
    "            i += 1\n",
    "        b.sort()\n",
    "        b.append(1.0)\n",
    "\n",
    "        if b[0] != 0.0:\n",
    "            b.insert(0,0.0)\n",
    "        else:\n",
    "            pg1,pg2 = pg2,pg1\n",
    "            #parent_indexes = (parent_indexes[1], parent_indexes[0])\n",
    "        \n",
    "        #pgams = list([*tuple(i for i in parent_indexes)]*int(len(b)/2))\n",
    "\n",
    "        #for left,right in zip(b[0:len(b)-1],b[1:len(b)]):\n",
    "        lenb = len(b)-1\n",
    "        for i in range(lenb):\n",
    "            left=b[i]\n",
    "            right=b[i+1]\n",
    "            rv = add_edge(left,right,pg1,\n",
    "                          next_offspring_id,&tables.edges)\n",
    "            pg1,pg2 = pg2,pg1\n",
    "            if rv != 0:\n",
    "                return rv\n",
    "    return 0\n",
    "\n",
    "def evolve(int N, int ngens, double theta, double rho, int gc, int seed):\n",
    "    nodes = msprime.NodeTable()\n",
    "    edges = msprime.EdgeTable()\n",
    "    mutations = msprime.MutationTable()\n",
    "    \n",
    "    cdef double mu = theta/<double>(4*N)\n",
    "    cdef double r = rho/<double>(4*N)\n",
    "    \n",
    "    cdef int rv\n",
    "    cdef size_t i, generation\n",
    "    cdef Tables tables\n",
    "    rv = init_Tables(&tables, seed)\n",
    "    if rv != 0:\n",
    "        free_Tables(&tables)\n",
    "        raise RuntimeError(\"could not initialize tables\")\n",
    "        \n",
    "    for i in range(2*<size_t>N):\n",
    "        nodes.add_row(time=0.0,\n",
    "                      flags=msprime.NODE_IS_SAMPLE)\n",
    "        \n",
    "    \n",
    "    cdef int32_t next_offspring_index, first_parental_index\n",
    "    next_offspring_index = len(nodes)\n",
    "    first_parental_index = 0\n",
    "    PARENT_DTYPE = np.int32\n",
    "    cdef np.ndarray[int32_t,ndim=1] parents\n",
    "    cdef size_t parent1, parent2,pindex\n",
    "    cdef int32_t p1g1, p1g2, p2g1, p2g2\n",
    "    cdef dict lookup = {}\n",
    "    for generation in range(1,<size_t>(ngens+1)):\n",
    "        for pindex in range(0,2*N,2):\n",
    "            parent1=<size_t>gsl_ran_flat(tables.rng,0.0,<double>N)\n",
    "            parent2=<size_t>gsl_ran_flat(tables.rng,0.0,<double>N)\n",
    "            p1g1 = first_parental_index + 2*parent1\n",
    "            p1g2 = p1g1 + 1\n",
    "            p2g1 = first_parental_index + 2*parent2\n",
    "            p2g2 = p2g1 + 1\n",
    "            \n",
    "            if gsl_rng_uniform(tables.rng) < 0.5:\n",
    "                p1g1, p1g2 = p1g2, p1g1\n",
    "            if gsl_rng_uniform(tables.rng) < 0.5:\n",
    "                p2g1, p2g2 = p2g2, p2g1\n",
    "                \n",
    "            rv = poisson_recombination(r,p1g1,p1g2,\n",
    "                                      next_offspring_index,\n",
    "                                      &tables)\n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during recombination\")\n",
    "                \n",
    "            rv = infsites(mu,generation,\n",
    "                         &tables,lookup)\n",
    "            \n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during mutation\")\n",
    "                \n",
    "            rv = add_node(<double>generation, &tables.nodes)\n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during adding nodes\")\n",
    "                \n",
    "            next_offspring_index += 1\n",
    "                \n",
    "            rv = poisson_recombination(r,p2g1,p2g2,\n",
    "                                      next_offspring_index,\n",
    "                                      &tables)\n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during recombination\")\n",
    "                \n",
    "            rv = infsites(mu,generation,\n",
    "                         &tables,lookup)\n",
    "            \n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during mutation\")\n",
    "                \n",
    "            rv = add_node(<double>generation, &tables.nodes)\n",
    "            if rv != 0:\n",
    "                free_Tables(&tables)\n",
    "                raise RuntimeError(\"error during adding nodes\")\n",
    "                \n",
    "            next_offspring_index += 1\n",
    "        first_parental_index += 2*N\n",
    "    \n",
    "    # Push first nodes times further back\n",
    "    nodes.set_columns(time=nodes.time + ngens +1,\n",
    "                     flags=nodes.flags)\n",
    "            \n",
    "    # Add our data to the tables\n",
    "    cdef double[:] timeview = <double[:tables.nodes.next_node]>tables.nodes.time\n",
    "    time=np.asarray(timeview,np.float)\n",
    "    time-=time.max()\n",
    "    time*=-1.0\n",
    "    nodes.append_columns(time=time,\n",
    "                         flags=np.ones(tables.nodes.next_node,np.uint32))\n",
    "    edges.append_columns(left=np.asarray(<double[:tables.edges.next_edge]>tables.edges.left),\n",
    "                        right=np.asarray(<double[:tables.edges.next_edge]>tables.edges.right),\n",
    "                        parent=np.asarray(<int32_t[:tables.edges.next_edge]>tables.edges.parent),\n",
    "                        child=np.asarray(<int32_t[:tables.edges.next_edge]>tables.edges.child))\n",
    "    \n",
    "    print(nodes.time.min(),nodes.time.max())\n",
    "\n",
    "    msprime.sort_tables(nodes=nodes,edges=edges)\n",
    "    \n",
    "    samples = np.where(nodes.time==0)[0]\n",
    "    \n",
    "    print(samples)\n",
    "    \n",
    "    msprime.simplify_tables(samples=samples.tolist(),\n",
    "                           nodes=nodes,\n",
    "                           edges=edges)\n",
    "                \n",
    "    free_Tables(&tables)\n",
    "    return msprime.load_tables(nodes=nodes,edges=edges)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "-0.0 10001.0\n",
      "[20000000 20000001 20000002 ... 20001997 20001998 20001999]\n",
      "CPU times: user 9.95 s, sys: 2.1 s, total: 12.1 s\n",
      "Wall time: 12.2 s\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "<msprime.trees.TreeSequence at 0x1030e1ba8>"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%%time\n",
    "evolve(1000, 10000, 500.0, 500.0, 10, 42)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "99794 160000\n",
      "0 10000\n",
      "done!\n"
     ]
    }
   ],
   "source": [
    "test_infsites()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "0 10000\n",
      "False\n",
      "False\n",
      "False\n",
      "False\n",
      "0\n",
      "20000 40000\n"
     ]
    }
   ],
   "source": [
    "test_add_edges()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python [conda root]",
   "language": "python",
   "name": "conda-root-py"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.5.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
